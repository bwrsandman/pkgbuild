# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/openmw

pkgname=openmw
pkgver=0.20.0
pkgrel=1
pkgdesc="An open-source engine reimplementation for the role-playing game Morrowind."
arch=('i686' 'x86_64')
url="http://www.openmw.org"
license=('GPL3' 'custom')

depends=('openal' 'ogre>=1.8.0-3' 'mygui>=3.2.0-2' 'bullet>=2.8.0' 'mpg123' 'libsndfile' 'qt')

makedepends=('cmake' 'boost>=1.5.0')

source=(http://openmw.googlecode.com/files/${pkgname}_${pkgver}.orig.tar.bz2)
sha1sums=('d0c0b867fcc051a2cb26aeb7b3585328f7777c3b')

conflicts=('openmw-git')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Binaries
  install -d -m755 "$pkgdir"/usr/bin
  install -m755 openmw "$pkgdir"/usr/bin/
  install -m755 omwlauncher "$pkgdir"/usr/bin/
  install -m755 esmtool "$pkgdir"/usr/bin/
  install -m775 mwiniimport "$pkgdir"/usr/bin

  # Config files
  # Replace resources location
  sed -i 's,resources=\./resources,resources=/usr/share/games/openmw/resources,' openmw.cfg || exit 1

  install -d -m755 "$pkgdir"/etc/openmw
  install -m644 openmw.cfg "$pkgdir"/etc/openmw/
  install -m644 settings-default.cfg "$pkgdir"/etc/openmw/
  install -m644 transparency-overrides.cfg "$pkgdir"/etc/openmw/

  # Desktop file and icon
  install -d -m755  "$pkgdir"/usr/share/applications
  install -m644 files/openmw.desktop "$pkgdir"/usr/share/applications/

  install -d -m755 "$pkgdir"/usr/share/pixmaps
  install -m644 apps/launcher/resources/images/openmw.png "$pkgdir"/usr/share/pixmaps/

  # Resources
  install -d -m755 "$pkgdir"/usr/share/games/openmw
  cp -r resources "$pkgdir"/usr/share/games/openmw/ || exit 1
  install -m644 launcher.qss "$pkgdir"/usr/share/games/openmw/resources

  # Custom Licences
  install -d -m755 "$pkgdir"/usr/share/licenses/openmw
  install -D -m644 "DejaVu Font License.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "Daedric Font License.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "OFL.txt" "$pkgdir/usr/share/licenses/openmw/"
  install -D -m644 "extern/shiny/License.txt" "$pkgdir/usr/share/licenses/openmw/Shiny License.txt"

}
