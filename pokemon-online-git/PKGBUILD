# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/pokemon-online-git

pkgname=pokemon-online-git
_gitname="pokemon-online"
pkgver=20130705
pkgrel=1
pkgdesc="Online Pokemon Battle Simulator"
arch=('i686' 'x86_64')
url="http://pokemon-online.eu/"
license=('GPL3')

depends=('libzip' 'phonon' 'qrencode' 'qt4' 'sqlite')
makedepends=('git' 'qt4')
conflicts=('pokemon-online')
provides=('pokemon-online')

source=('git://github.com/po-devs/pokemon-online.git'
        'git://github.com/po-devs/pkg-pokemononline.git')
sha1sums=('SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format="%cd" --date=short | tr -d '-'
}

prepare() {
  cd "$srcdir/$_gitname"
  patch -p1 < "$srcdir"/pkg-pokemononline/debian/patches/disable_qtwebsocket.diff
  sed -i "s,\"myplugins\",\"/usr/lib/pokemon-online/clientplugins\"," src/Teambuilder/pluginmanager.cpp
  sed -i "s,\"serverplugins\",\"/usr/lib/pokemon-online/serverplugins\"," src/Server/pluginmanager.cpp
}

build() {
  cd "$srcdir/$_gitname"
  qmake-qt4 "CONFIG += po_server po_client debian_package" 
  make
}

package() {
  cd "$srcdir/$_gitname"

  # Binaries
  install -d -m755 "$pkgdir"/usr/bin/
  install -m755 bin/Pokemon-Online "$pkgdir"/usr/bin/Pokemon-Online
  install -m755 bin/Server "$pkgdir"/usr/bin/Pokemon-Online-Server

  # Libraries
  install -d -m755 "$pkgdir"/usr/lib/
  cp -P bin/libpo-battlemanager.so "$pkgdir"/usr/lib
  cp -P bin/libpo-battlemanager.so.* "$pkgdir"/usr/lib
  cp -P bin/libpo-pokemoninfo.so "$pkgdir"/usr/lib
  cp -P bin/libpo-pokemoninfo.so.* "$pkgdir"/usr/lib
  cp -P bin/libpo-utilities.so "$pkgdir"/usr/lib
  cp -P bin/libpo-utilities.so.* "$pkgdir"/usr/lib

  #install -m755 bin/myplugins/lib*.so "$pkgdir"/usr/lib/pokemon-online/clientplugins
  #install -m755 bin/myplugins/lib*.so.* "$pkgdir"/usr/lib/pokemon-online/clientplugins

  
  # Data
  install -d -m755 "$pkgdir"/usr/share/games/pokemon-online
  install -m644 bin/languages.txt "$pkgdir"/usr/share/games/pokemon-online
  install -m644 bin/version.ini "$pkgdir"/usr/share/games/pokemon-online
  cp -r bin/db "$pkgdir"/usr/share/games/pokemon-online
  cp -r bin/qml "$pkgdir"/usr/share/games/pokemon-online
  cp -r bin/Music "$pkgdir"/usr/share/games/pokemon-online
  cp -r bin/Themes "$pkgdir"/usr/share/games/pokemon-online
  cp -r bin/trans "$pkgdir"/usr/share/games/pokemon-online

  cd "$srcdir"/pkg-pokemononline
  install -d -m755 "$pkgdir"/usr/share/pixmaps
  install -m644 debian/pokemon-online.xpm "$pkgdir"/usr/share/pixmaps

  install -d -m755 "$pkgdir"/usr/share/applications
  install -m644 debian/pokemon-online-client.desktop "$pkgdir"/usr/share/applications
  install -m644 debian/pokemon-online-server.desktop "$pkgdir"/usr/share/applications
}
