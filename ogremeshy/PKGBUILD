# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/ogremeshy

pkgname=ogremeshy
pkgver=1.5
pkgrel=1
pkgdesc="A tool for viewing OGRE mesh files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ogremeshy/"
license=('GPL3')

depends=('ogre' 'wxgtk')
makedepends=('cmake')

source=(http://sourceforge.net/projects/$pkgname/files/OgreMeshy.$pkgver.src.tar.bz2
        ogremeshy.desktop) 
md5sums=('53cdaf06dcd70337a4961b9554e3a1c3'
         'a8ec805fc905323d24532d841b442c1c')

prepare() {
  cd "${srcdir}"/OgreMeshy

  cp CMakeLists{.old.1.8,}.txt

  # Tell OgreMeshy where to find the OGRE plugins
  sed -i "s/PluginFolder=\/opt\/ogremeshy\/Plugins/PluginFolder=\/usr\/lib\/OGRE/g" \
        bin/Release_Linux/Plugins.cfg

  sed -i '58 i \
	find_package(Boost COMPONENTS system filesystem)' CMakeLists.txt
  sed -i '59 i \
	include_directories(${Boost_INCLUDE_DIR})' CMakeLists.txt
  sed -i '60 i \
	set(OGRE_LIBRARIES ${OGRE_LIBRARIES} ${Boost_LIBRARIES})' CMakeLists.txt
  sed -i '107 i \
	TARGET_LINK_LIBRARIES( OgreMeshy ${OGRE_LIBRARIES} )' CMakeLists.txt
}

build() {
  cd "$srcdir"/OgreMeshy
  cmake -DCMAKE_INSTALL_PREFIX="/opt/$pkgname"
  make 
}

package() {
  cd "$srcdir"/OgreMeshy
  make DESTDIR="${pkgdir}" install

  # Use a startup script to force launching in /opt/$pkgname
  install -d "$pkgdir/usr/bin/"
  echo \
"#!/bin/bash
cd /opt/\"$pkgname\"
exec ./OgreMeshy \"$@\"" > "$pkgdir/usr/bin/$pkgname"
  chmod a+x "$pkgdir/usr/bin/$pkgname"
  
  # Install resources
  install -d "$pkgdir/opt/$pkgname"/Resources

  cp bin/Release_Linux/Plugins.cfg "$pkgdir/opt/$pkgname"

  cp -r scripts/Resources/{Icons,Fonts} "$pkgdir/opt/$pkgname"/Resources
  cp -r scripts/Resources/Other    "$pkgdir/opt/$pkgname"/Resources/Models

  cp scripts/Resources/Blender/Axis.{material,mesh} "$pkgdir/opt/$pkgname"/Resources/Models
  cp scripts/Resources/Blender/Bones/{BoneMesh.material,Bones.png,BoneTip.mesh,BoneGlobe.mesh} "$pkgdir/opt/$pkgname"/Resources/Models

  install -d "$pkgdir/usr/share/applications"
  cp "$srcdir/ogremeshy.desktop" "$pkgdir/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
