# Maintainer: Sandy Carter <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/ogremeshy

pkgname=ogremeshy
pkgver=1.5
pkgrel=1
pkgdesc="A tool for viewing OGRE mesh files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ogremeshy/"
license=('GPL3')

depends=('ogre' 'wxgtk')
makedepends=('cmake')

source=(http://sourceforge.net/projects/$pkgname/files/OgreMeshy.$pkgver.src.tar.bz2
        ogremeshy.desktop
        boost_fix.patch
        install_resources.patch)
sha1sums=('11fa0a1f6cd770d22f0ee0dd637985255f02927b'
          '4441f9aed2fb728ef3788023c90cc7a35ce753c9'
	  '4d3a077fe6754c9cdc34f151a880844f710024b4'
	  'fb81a98785232a85b9efad151e11751d542c9ca7')

prepare() {
  cd "${srcdir}"/OgreMeshy

  mv CMakeLists{.old.1.8,}.txt
  cp "${srcdir}"/ogremeshy.desktop bin/Release_Linux

  # Tell OgreMeshy where to find the OGRE plugins
  sed -i "s/PluginFolder=\/opt\/ogremeshy\/Plugins/PluginFolder=\/usr\/lib\/OGRE/g" \
        bin/Release_Linux/Plugins.cfg

  patch < "${srcdir}"/boost_fix.patch
  patch < "${srcdir}"/install_resources.patch
}

build() {
  cd "$srcdir"/OgreMeshy
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release
  make 
}

package() {
  cd "$srcdir"/OgreMeshy
  make DESTDIR="${pkgdir}" install

  # Use a startup script to force launching in /opt/$pkgname
  install -d "$pkgdir/usr/bin/"
  echo \
"#!/bin/sh
cd /opt/\"$pkgname\"
exec ./OgreMeshy" > "$pkgdir/usr/bin/$pkgname"
  chmod a+x "$pkgdir/usr/bin/$pkgname"
}
